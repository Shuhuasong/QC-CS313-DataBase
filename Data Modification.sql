USE TSQLV4;

--Data Modification

--Inserting Data

--1.1The INSERT VALUES Statement: to insert rows into a table based on specified values
drop table if exists dbo.Orders;

create table dbo.Orders
(
    orderid int      not NULL
       constraint PK_Orders primary key,  --!!!!!
    orderdate date   not NULL
       constraint dft_orderdate default(sysdatetime()),--!!!!!
    empid    int     not NULL,
    custid   varchar(10) not NULL
);

insert into dbo.Orders(orderid, orderdate, empid, custid)
     values
     (10009, '20160215', 1, '3'),
     --(10001, '20160212', 3, 'A'), //
     (10002, '20160213', 2, 'B'), --implicity cast from string to date
     (10003, '20160214', 1, 'C'),
     (10004, '20160215', 1, 'D'),

     (10005, '20160216', 3, 'E');

insert into dbo.Orders(orderid, empid, custid)
     values
     (10006,  3, 'A') --use systemdatetime;

select *
from dbo.Orders

--1.2 The INSERT SELECT Statement:  inserts a set of rows returned by a SELECT query into a target table.
--option of specifying the target column names,
--is performed as an atomic operation,so if any row fails to enter the target table, 
--none of the rows enters the table.
-- set IDENTITY
insert into dbo.Orders(orderid, orderdate, empid, custid) --Which database I need to choose
     select orderid, orderdate, empid, custid
     from Sales.Orders
     where shipcountry = 'UK';

--1.3 The INSERT EXEC Statement: to insert a result set returned from a stored procedure or a dynamic
--SQL batch into a target table.

drop proc if exists Sales.GetOrders;
GO
create proc Sales.GetOrders
    @country as nvarchar(40)
AS

select orderid, orderdate, empid, custid
from Sales.Orders
where shipcountry = @country

--To test the stored procedure, execute it with the input country France.
exec Sales.GetOrders @country = N'France';

--By using an INSERT EXEC statement, you can direct the result set returned from the procedure to
--the dbo.Orders table.
insert into dbo.Orders(orderid, orderdate, empid, custid)
exec Sales.GetOrders @country = N'France';

--1.4 The SELECT INTO Statement: statement that creates a target table and populates
--it with the result set of a query.

--the following code creates a table called dbo.Orders and populates it with all rows from the Sales.Orders table.
drop table if exists dbo.Orders;

select orderid, orderdate, empid, custid
into dbo.Orders
from Sales.Orders;

select *
from dbo.Orders

--There are four things that the statement does not copy from the source: constraints, indexes,
--triggers, and permissions. If you need those in the target, you will need to create them yourself.
--use a SELECT INTO statement with set operations

--following returning locations where there are customers but not employees.
drop table if exists dbo.Locations;

select country, region, city
into dbo.Locations
from Sales.Customers

EXCEPT

select country, region, city
from HR.Employees

select *
from dbo.Locations

--The BULK INSERT Statement ---????? This one cannot work
--statement to insert into an existing table data originating from a file.
--the following code bulk inserts the contents of the file c:\temp\orders.txt into the
--table dbo.Orders, specifying that the data file type is char, the field terminator is a comma, and the
--row terminator is the newline character.

--remove the foreign key before I move 

bulk insert dbo.Orders from '/Users/shuhuasong/Documents/2019-Summer/2019-Fall/CSIS331-DataBase/Homework/Chapter8/orders.txt '
WITH
(
    datafiletype  = 'char',
    fieldterminator = ',',
    rowterminator = '\n'
);

--The Identity Property and the Sequence Object
--SQL Server supports two built-in solutions to automatically generate keys:
--The sequence resolves many of the identity property’s limitations

--Identity
--This property generates values automatically
--with an identity property using 1 as the seed and 1 as the increment.
drop table if exists dbo.T1
create table dbo.T1
(
    keycol int  not null identity(1,1)   
       constraint pk_T1 primary key,
    datacol varchar(10) not null --a character string column
       constraint CHK_T1_datacol check(datacol like '[A-Za-z]%') --?????what is this?
       --data is restricted with a CHECK constraint to strings starting with an alphabetical character.
);

--should completely ignore the identity column,cause SQL Server produced the values for keycol automatically
insert into dbo.T1(datacol) values('AAAAA');
insert into dbo.T1(datacol) values('CCCCC');
insert into dbo.T1(datacol) values('BBBBB');

SELECT * 
FROM dbo.T1;

--the following query selects the identity column from T1 by using the generic form.
select $identity 
from dbo.T1

-- SCOPE_IDENTITY returns the last identity value generated by the session in the current scope 
declare @new_key as int;
insert into dbo.T1(datacol)  values('AAAAA');

set @new_key = SCOPE_IDENTITY();
select @new_key  as new_key


--both @@identity and SCOPE_IDENTITY return the last identity value produced by the current session.
--Run from another connection
--use the IDENT_CURRENT function: to know the current identity value in a table (the last value produced) regardless of session
--current identity value in a table (the last value produced) regardless of session
select 
   SCOPE_IDENTITY() as [SCOPE_IDENTITY],--because no identity values were created in the session
   @@identity as [@@identity],
   IDENT_CURRENT(N'dbo.T1') as [IDENT_CURRENT];
GO 

--the following INSERT statement, which contradicts the CHECK constraint defined in the table.
--INSERT INTO dbo.T1(datacol) VALUES('12345'); --Why this line cannot insert??????

insert into dbo.T1(datacol) values('EEEEE'); --it skip the the number 5 identity
select *
from dbo.T1

--identity property is that you cannot add it to an existing column
--or remove it from an existing column; you can only define the property along with a column as part
--of a CREATE TABLE statement or an ALTER TABLE statement that adds a new column.

-- Using IDENTITY_INSERT: to explicitly specify your own values for the identity colum
--the following code demonstrates how to insert a row into T1 with the explicit value 5 in keycol.
set identity_insert dbo.T1 on;
insert into dbo.T1(keycol, datacol) values(5, 'FFFFFFF');
--set identity_insert dbo.T1 off;

---------------------------------------------------------------------
-- Sequence Object
---------------------------------------------------------------------
 
-- create sequence and request value
--explicit about the type, minimum value, and cycling option,
DROP SEQUENCE IF EXISTS dbo.SeqOrderIDs;
 
CREATE SEQUENCE dbo.SeqOrderIDs AS INT
  MINVALUE 1
  CYCLE;
 
/*
ALTER SEQUENCE dbo.SeqOrderIDs
  RESTART WITH <constant>
  INCREMENT BY <constant>
  MINVALUE <constant> | NO MINVALUE
  MAXVALUE <constant> | NO MAXVALUE
  CYCLE | NO CYCLE
  CACHE <constant> | NO CACHE;
*/
--change the current sequence definition
ALTER SEQUENCE dbo.SeqOrderIDs
  NO CYCLE;
 
-- use
--To generate a new sequence value, you need to invoke the function NEXT VALUE FOR <sequencename>.
--can store the result of the function in a variable, and then use it wherever you like.
SELECT NEXT VALUE FOR dbo.SeqOrderIDs;

 
DROP TABLE IF EXISTS dbo.T1;
 
CREATE TABLE dbo.T1
(
  keycol  INT         NOT NULL
    CONSTRAINT PK_T1 PRIMARY KEY,
  datacol VARCHAR(10) NOT NULL
);
GO
 
--NEXT VALUE FOR <sequence name> :To generate a new sequence value, you need to invoke the function
--the following code generates a new sequence value, stores it in a variable, and then uses the variable
--in an INSERT statement to insert a row into the table.
DECLARE @neworderid AS INT = NEXT VALUE FOR dbo.SeqOrderIDs;
INSERT INTO dbo.T1(keycol, datacol) VALUES(@neworderid, 'a');
 
SELECT * FROM dbo.T1;
GO
 
--If you don’t need to generate the new sequence value before using it, you can specify the NEXT
--VALUE FOR function directly as part of your INSERT statement
INSERT INTO dbo.T1(keycol, datacol)
  VALUES(NEXT VALUE FOR dbo.SeqOrderIDs, 'b');
 
SELECT * FROM dbo.T1;
GO

--generate new sequence values in an UPDATE statement
UPDATE dbo.T1
  SET keycol = NEXT VALUE FOR dbo.SeqOrderIDs;
 
SELECT * FROM dbo.T1;
GO

-- to find the current sequence value in the SeqOrderIDs sequence, you would use the following code.
-- info
SELECT current_value
FROM sys.sequences
WHERE OBJECT_ID = OBJECT_ID(N'dbo.SeqOrderIDs');
 
-- order
--to control the order of the assigned sequence values in a multi-row insert by using an OVER clause 
--similar to the one window functions use.
INSERT INTO dbo.T1(keycol, datacol)
  SELECT
    NEXT VALUE FOR dbo.SeqOrderIDs OVER(ORDER BY hiredate),
    LEFT(firstname, 1) + LEFT(lastname, 1)
  FROM HR.Employees;
 
SELECT * FROM dbo.T1;
GO
 
--Another extension allows the use of the NEXT VALUE FOR function in a default constraint
ALTER TABLE dbo.T1
  ADD CONSTRAINT DFT_T1_keycol
    DEFAULT (NEXT VALUE FOR dbo.SeqOrderIDs)
    FOR keycol;
 
INSERT INTO dbo.T1(datacol) VALUES('c');
 
SELECT * FROM dbo.T1;
GO
 
-- range
--to allocate a whole range of sequence values at once by using a stored procedure called sp_sequence_get_range.
--an example of calling the
--procedure and asking for a range of 1,000 sequence values.
DECLARE @first AS SQL_VARIANT;
 
EXEC sys.sp_sequence_get_range
  @sequence_name     = N'dbo.SeqOrderIDs',
  @range_size        = 1000,
  @range_first_value = @first OUTPUT ;
 
SELECT @first;
GO
 
-- cleanup
DROP TABLE IF EXISTS dbo.T1;
DROP SEQUENCE IF EXISTS dbo.SeqOrderIDs;
GO
 
---------------------------------------------------------------------
-- Deleting Data, fully logged
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.Orders, dbo.Customers;
 
CREATE TABLE dbo.Customers
(
  custid       INT          NOT NULL,
  companyname  NVARCHAR(40) NOT NULL,
  contactname  NVARCHAR(30) NOT NULL,
  contacttitle NVARCHAR(30) NOT NULL,
  address      NVARCHAR(60) NOT NULL,
  city         NVARCHAR(15) NOT NULL,
  region       NVARCHAR(15) NULL,
  postalcode   NVARCHAR(10) NULL,
  country      NVARCHAR(15) NOT NULL,
  phone        NVARCHAR(24) NOT NULL,
  fax          NVARCHAR(24) NULL,
  CONSTRAINT PK_Customers PRIMARY KEY(custid)
);
 
CREATE TABLE dbo.Orders
(
  orderid        INT          NOT NULL,
  custid         INT          NULL,
  empid          INT          NOT NULL,
  orderdate      DATE         NOT NULL,
  requireddate   DATE         NOT NULL,
  shippeddate    DATE         NULL,
  shipperid      INT          NOT NULL,
  freight        MONEY        NOT NULL
    CONSTRAINT DFT_Orders_freight DEFAULT(0),
  shipname       NVARCHAR(40) NOT NULL,
  shipaddress    NVARCHAR(60) NOT NULL,
  shipcity       NVARCHAR(15) NOT NULL,
  shipregion     NVARCHAR(15) NULL,
  shippostalcode NVARCHAR(10) NULL,
  shipcountry    NVARCHAR(15) NOT NULL,
  CONSTRAINT PK_Orders PRIMARY KEY(orderid),
  CONSTRAINT FK_Orders_Customers FOREIGN KEY(custid)
    REFERENCES dbo.Customers(custid)
);
GO
 
INSERT INTO dbo.Customers SELECT * FROM Sales.Customers;
INSERT INTO dbo.Orders SELECT * FROM Sales.Orders;
 
---------------------------------------------------------------------
-- DELETE Statement
---------------------------------------------------------------------
 --the following statement deletes, from the dbo.Orders table, all orders that were
--placed prior to 2015.
DELETE FROM dbo.Orders
WHERE orderdate < '20150101';
GO
 
---------------------------------------------------------------------
-- TRUNCATE
--statement deletes all rows from a table
---------------------------------------------------------------------
 
-- Code to create the table T1 (partitioned) if you want to run the examples that follow
DROP TABLE IF EXISTS dbo.T1;
IF EXISTS (SELECT * FROM sys.partition_schemes WHERE name = N'PS1') DROP PARTITION SCHEME PS1;
IF EXISTS (SELECT * FROM sys.partition_functions WHERE name = N'PF1') DROP PARTITION FUNCTION PF1;
 
CREATE PARTITION FUNCTION PF1 (INT) AS RANGE LEFT FOR VALUES (10, 20, 30, 40, 50, 60, 70, 80, 90, 100, 110, 120);
CREATE PARTITION SCHEME PS1 AS PARTITION PF1 ALL TO ([PRIMARY]);
 
CREATE TABLE dbo.T1
(
  keycol INT NOT NULL
    CONSTRAINT PK_T1 PRIMARY KEY,
  datacol INT NOT NULL
) ON PS1(keycol);
GO
 
-- TRUNCATE statement examples
TRUNCATE TABLE dbo.T1;
GO
 
TRUNCATE TABLE dbo.T1 WITH ( PARTITIONS(1, 3, 5, 7 TO 10) );
GO
 
-- Cleanup
DROP TABLE IF EXISTS dbo.T1;
IF EXISTS (SELECT * FROM sys.partition_schemes WHERE name = N'PS1') DROP PARTITION SCHEME PS1;
IF EXISTS (SELECT * FROM sys.partition_functions WHERE name = N'PF1') DROP PARTITION FUNCTION PF1;
 
---------------------------------------------------------------------
-- DELETE Based on Join
---------------------------------------------------------------------
 
-- Using a join
DELETE FROM O
FROM dbo.Orders AS O
  INNER JOIN dbo.Customers AS C
    ON O.custid = C.custid
WHERE C.country = N'USA';
 
-- Using a subquery
DELETE FROM dbo.Orders
WHERE EXISTS
  (SELECT *
   FROM dbo.Customers AS C
   WHERE Orders.custid = C.custid
     AND C.country = N'USA');
 
-- cleanup
DROP TABLE IF EXISTS dbo.Orders, dbo.Customers;
 
---------------------------------------------------------------------
-- Updating Data
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.OrderDetails, dbo.Orders;
 
CREATE TABLE dbo.Orders
(
  orderid        INT          NOT NULL,
  custid         INT          NULL,
  empid          INT          NOT NULL,
  orderdate      DATE         NOT NULL,
  requireddate   DATE         NOT NULL,
  shippeddate    DATE         NULL,
  shipperid      INT          NOT NULL,
  freight        MONEY        NOT NULL
    CONSTRAINT DFT_Orders_freight DEFAULT(0),
  shipname       NVARCHAR(40) NOT NULL,
  shipaddress    NVARCHAR(60) NOT NULL,
  shipcity       NVARCHAR(15) NOT NULL,
  shipregion     NVARCHAR(15) NULL,
  shippostalcode NVARCHAR(10) NULL,
  shipcountry    NVARCHAR(15) NOT NULL,
  CONSTRAINT PK_Orders PRIMARY KEY(orderid)
);
 
CREATE TABLE dbo.OrderDetails
(
  orderid   INT           NOT NULL,
  productid INT           NOT NULL,
  unitprice MONEY         NOT NULL
    CONSTRAINT DFT_OrderDetails_unitprice DEFAULT(0),
  qty       SMALLINT      NOT NULL
    CONSTRAINT DFT_OrderDetails_qty DEFAULT(1),
  discount  NUMERIC(4, 3) NOT NULL
    CONSTRAINT DFT_OrderDetails_discount DEFAULT(0),
  CONSTRAINT PK_OrderDetails PRIMARY KEY(orderid, productid),
  CONSTRAINT FK_OrderDetails_Orders FOREIGN KEY(orderid)
    REFERENCES dbo.Orders(orderid),
  CONSTRAINT CHK_discount  CHECK (discount BETWEEN 0 AND 1),
  CONSTRAINT CHK_qty  CHECK (qty > 0),
  CONSTRAINT CHK_unitprice CHECK (unitprice >= 0)
);
GO
 
INSERT INTO dbo.Orders SELECT * FROM Sales.Orders;
INSERT INTO dbo.OrderDetails SELECT * FROM Sales.OrderDetails;
 
---------------------------------------------------------------------
-- UPDATE Statement
---------------------------------------------------------------------
 
-- Code to create tables T1 and T2 if you want to run the examples that follow
DROP TABLE IF EXISTS dbo.T1, dbo.T2;
 
CREATE TABLE dbo.T1
(
  keycol INT NOT NULL
    CONSTRAINT PK_T1 PRIMARY KEY,
  col1 INT NOT NULL,
  col2 INT NOT NULL,
  col3 INT NOT NULL,
  col4 VARCHAR(10) NOT NULL
);
 
CREATE TABLE dbo.T2
(
  keycol INT NOT NULL
    CONSTRAINT PK_T2 PRIMARY KEY,
  col1 INT NOT NULL,
  col2 INT NOT NULL,
  col3 INT NOT NULL,
  col4 VARCHAR(10) NOT NULL
);
GO
 
-- UPDATE examples
UPDATE dbo.OrderDetails
  SET discount = discount + 0.05
WHERE productid = 51;
 
-- Compund assignment operators
UPDATE dbo.OrderDetails
  SET discount += 0.05
WHERE productid = 51;
GO
 
UPDATE dbo.T1
  SET col1 = col1 + 10, col2 = col1 + 10;
GO
 
UPDATE dbo.T1
  SET col1 = col2, col2 = col1;
GO
 
---------------------------------------------------------------------
-- UPDATE Based on Join
---------------------------------------------------------------------
 
-- Listing 8-1 Update Based on Join
UPDATE OD
  SET discount += 0.05
FROM dbo.OrderDetails AS OD
  INNER JOIN dbo.Orders AS O
    ON OD.orderid = O.orderid
WHERE O.custid = 1;
 
UPDATE dbo.OrderDetails
  SET discount += 0.05
WHERE EXISTS
  (SELECT * FROM dbo.Orders AS O
   WHERE O.orderid = OrderDetails.orderid
     AND custid = 1);
GO
 
UPDATE T1
  SET col1 = T2.col1,
      col2 = T2.col2,
      col3 = T2.col3
FROM dbo.T1 JOIN dbo.T2
  ON T2.keycol = T1.keycol
WHERE T2.col4 = 'ABC';
GO
 
UPDATE dbo.T1
  SET col1 = (SELECT col1
              FROM dbo.T2
              WHERE T2.keycol = T1.keycol),
               
      col2 = (SELECT col2
              FROM dbo.T2
              WHERE T2.keycol = T1.keycol),
       
      col3 = (SELECT col3
              FROM dbo.T2
              WHERE T2.keycol = T1.keycol)
WHERE EXISTS
  (SELECT *
   FROM dbo.T2
   WHERE T2.keycol = T1.keycol
     AND T2.col4 = 'ABC');
GO
 
/*
UPDATE dbo.T1
 
  SET (col1, col2, col3) =
 
      (SELECT col1, col2, col3
       FROM dbo.T2
       WHERE T2.keycol = T1.keycol)
        
WHERE EXISTS
  (SELECT *
   FROM dbo.T2
   WHERE T2.keycol = T1.keycol
     AND T2.col4 = 'ABC');
*/     
GO
 
-- Cleanup
DROP TABLE IF EXISTS dbo.T1, dbo.T2;
         
---------------------------------------------------------------------
-- Assignment UPDATE
---------------------------------------------------------------------
 
-- Custom Sequence
DROP TABLE IF EXISTS dbo.MySequences;
 
CREATE TABLE dbo.MySequences
(
  id VARCHAR(10) NOT NULL
    CONSTRAINT PK_Sequences PRIMARY KEY(id),
  val INT NOT NULL
);
INSERT INTO dbo.MySequences VALUES('SEQ1', 0);
GO
 
DECLARE @nextval AS INT;
 
UPDATE dbo.MySequences
  SET @nextval = val += 1
WHERE id = 'SEQ1';
 
SELECT @nextval;
 
-- cleanup
DROP TABLE IF EXISTS dbo.MySequences;
 
---------------------------------------------------------------------
-- Merging Data
---------------------------------------------------------------------
 
-- Listing 8-2 Code that Creates and Populates Customers and CustomersStage
DROP TABLE IF EXISTS dbo.Customers, dbo.CustomersStage;
GO
 
CREATE TABLE dbo.Customers
(
  custid      INT         NOT NULL,
  companyname VARCHAR(25) NOT NULL,
  phone       VARCHAR(20) NOT NULL,
  address     VARCHAR(50) NOT NULL,
  CONSTRAINT PK_Customers PRIMARY KEY(custid)
);
 
INSERT INTO dbo.Customers(custid, companyname, phone, address)
VALUES
  (1, 'cust 1', '(111) 111-1111', 'address 1'),
  (2, 'cust 2', '(222) 222-2222', 'address 2'),
  (3, 'cust 3', '(333) 333-3333', 'address 3'),
  (4, 'cust 4', '(444) 444-4444', 'address 4'),
  (5, 'cust 5', '(555) 555-5555', 'address 5');
 
CREATE TABLE dbo.CustomersStage
(
  custid      INT         NOT NULL,
  companyname VARCHAR(25) NOT NULL,
  phone       VARCHAR(20) NOT NULL,
  address     VARCHAR(50) NOT NULL,
  CONSTRAINT PK_CustomersStage PRIMARY KEY(custid)
);
 
INSERT INTO dbo.CustomersStage(custid, companyname, phone, address)
VALUES
  (2, 'AAAAA', '(222) 222-2222', 'address 2'),
  (3, 'cust 3', '(333) 333-3333', 'address 3'),
  (5, 'BBBBB', 'CCCCC', 'DDDDD'),
  (6, 'cust 6 (new)', '(666) 666-6666', 'address 6'),
  (7, 'cust 7 (new)', '(777) 777-7777', 'address 7');
 
-- Query tables
SELECT * FROM dbo.Customers;
 
SELECT * FROM dbo.CustomersStage;
 
-- MERGE Example 1: Update existing, add missing
MERGE INTO dbo.Customers AS TGT
USING dbo.CustomersStage AS SRC
  ON TGT.custid = SRC.custid
WHEN MATCHED THEN
  UPDATE SET
    TGT.companyname = SRC.companyname,
    TGT.phone = SRC.phone,
    TGT.address = SRC.address
WHEN NOT MATCHED THEN
  INSERT (custid, companyname, phone, address)
  VALUES (SRC.custid, SRC.companyname, SRC.phone, SRC.address);
 
-- Query table
SELECT * FROM dbo.Customers; 
 
-- MERGE Example 2: Update existing, add missing, delete missing in source
MERGE dbo.Customers AS TGT
USING dbo.CustomersStage AS SRC
  ON TGT.custid = SRC.custid
WHEN MATCHED THEN
  UPDATE SET
    TGT.companyname = SRC.companyname,
    TGT.phone = SRC.phone,
    TGT.address = SRC.address
WHEN NOT MATCHED THEN
  INSERT (custid, companyname, phone, address)
  VALUES (SRC.custid, SRC.companyname, SRC.phone, SRC.address)
WHEN NOT MATCHED BY SOURCE THEN
  DELETE;
 
-- Query table
SELECT * FROM dbo.Customers; 
 
-- MERGE Example 3: Update existing that changed, add missing
MERGE dbo.Customers AS TGT
USING dbo.CustomersStage AS SRC
  ON TGT.custid = SRC.custid
WHEN MATCHED AND
       (   TGT.companyname <> SRC.companyname
        OR TGT.phone       <> SRC.phone
        OR TGT.address     <> SRC.address) THEN
  UPDATE SET
    TGT.companyname = SRC.companyname,
    TGT.phone = SRC.phone,
    TGT.address = SRC.address
WHEN NOT MATCHED THEN
  INSERT (custid, companyname, phone, address)
  VALUES (SRC.custid, SRC.companyname, SRC.phone, SRC.address);
 
---------------------------------------------------------------------
-- Modifying Data through Table Expressions
---------------------------------------------------------------------
 
UPDATE OD
  SET discount += 0.05
FROM dbo.OrderDetails AS OD
  INNER JOIN dbo.Orders AS O
    ON OD.orderid = O.orderid
WHERE O.custid = 1;
 
-- CTE
WITH C AS
(
  SELECT custid, OD.orderid,
    productid, discount, discount + 0.05 AS newdiscount
  FROM dbo.OrderDetails AS OD
    INNER JOIN dbo.Orders AS O
      ON OD.orderid = O.orderid
  WHERE O.custid = 1
)
UPDATE C
  SET discount = newdiscount;
 
-- Derived Table
UPDATE D
  SET discount = newdiscount
FROM ( SELECT custid, OD.orderid,
         productid, discount, discount + 0.05 AS newdiscount
       FROM dbo.OrderDetails AS OD
         INNER JOIN dbo.Orders AS O
           ON OD.orderid = O.orderid
       WHERE O.custid = 1 ) AS D;
 
-- Update with row numbers
DROP TABLE IF EXISTS dbo.T1;
CREATE TABLE dbo.T1(id INT NOT NULL IDENTITY PRIMARY KEY, col1 INT, col2 INT);
GO
 
INSERT INTO dbo.T1(col1) VALUES(20),(10),(30);
 
SELECT * FROM dbo.T1;
GO
 
UPDATE dbo.T1
  SET col2 = ROW_NUMBER() OVER(ORDER BY col1);
 
/*
Msg 4108, Level 15, State 1, Line 672
Windowed functions can only appear in the SELECT or ORDER BY clauses.
*/
GO
   
WITH C AS
(
  SELECT col1, col2, ROW_NUMBER() OVER(ORDER BY col1) AS rownum
  FROM dbo.T1
)
UPDATE C
  SET col2 = rownum;
 
SELECT col1, col2 FROM dbo.T1;
 
---------------------------------------------------------------------
-- Modifications with TOP and OFFSET-FETCH
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.OrderDetails, dbo.Orders;
 
CREATE TABLE dbo.Orders
(
  orderid        INT          NOT NULL,
  custid         INT          NULL,
  empid          INT          NOT NULL,
  orderdate      DATE         NOT NULL,
  requireddate   DATE         NOT NULL,
  shippeddate    DATE         NULL,
  shipperid      INT          NOT NULL,
  freight        MONEY        NOT NULL
    CONSTRAINT DFT_Orders_freight DEFAULT(0),
  shipname       NVARCHAR(40) NOT NULL,
  shipaddress    NVARCHAR(60) NOT NULL,
  shipcity       NVARCHAR(15) NOT NULL,
  shipregion     NVARCHAR(15) NULL,
  shippostalcode NVARCHAR(10) NULL,
  shipcountry    NVARCHAR(15) NOT NULL,
  CONSTRAINT PK_Orders PRIMARY KEY(orderid)
);
GO
 
INSERT INTO dbo.Orders SELECT * FROM Sales.Orders;
 
DELETE TOP(50) FROM dbo.Orders;
 
UPDATE TOP(50) dbo.Orders
  SET freight += 10.00;
 
-- TOP
WITH C AS
(
  SELECT TOP (50) *
  FROM dbo.Orders
  ORDER BY orderid
)
DELETE FROM C;
 
WITH C AS
(
  SELECT TOP (50) *
  FROM dbo.Orders
  ORDER BY orderid DESC
)
UPDATE C
  SET freight += 10.00;
 
-- OFFSET-FETCH
WITH C AS
(
  SELECT *
  FROM dbo.Orders
  ORDER BY orderid
  OFFSET 0 ROWS FETCH NEXT 50 ROWS ONLY
)
DELETE FROM C;
 
WITH C AS
(
  SELECT *
  FROM dbo.Orders
  ORDER BY orderid DESC
  OFFSET 0 ROWS FETCH NEXT 50 ROWS ONLY
)
UPDATE C
  SET freight += 10.00;
 
---------------------------------------------------------------------
-- OUTPUT
---------------------------------------------------------------------
 
---------------------------------------------------------------------
-- INSERT with OUTPUT
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.T1;
GO
 
CREATE TABLE dbo.T1
(
  keycol  INT          NOT NULL IDENTITY(1, 1) CONSTRAINT PK_T1 PRIMARY KEY,
  datacol NVARCHAR(40) NOT NULL
);
 
INSERT INTO dbo.T1(datacol)
  OUTPUT inserted.keycol, inserted.datacol
    SELECT lastname
    FROM HR.Employees
    WHERE country = N'USA';
GO
 
DECLARE @NewRows TABLE(keycol INT, datacol NVARCHAR(40));
 
INSERT INTO dbo.T1(datacol)
  OUTPUT inserted.keycol, inserted.datacol
  INTO @NewRows(keycol, datacol)
    SELECT lastname
    FROM HR.Employees
    WHERE country = N'UK';
 
SELECT * FROM @NewRows;
 
---------------------------------------------------------------------
-- DELETE with OUTPUT
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.Orders;
 
CREATE TABLE dbo.Orders
(
  orderid        INT          NOT NULL,
  custid         INT          NULL,
  empid          INT          NOT NULL,
  orderdate      DATE         NOT NULL,
  requireddate   DATE         NOT NULL,
  shippeddate    DATE         NULL,
  shipperid      INT          NOT NULL,
  freight        MONEY        NOT NULL
    CONSTRAINT DFT_Orders_freight DEFAULT(0),
  shipname       NVARCHAR(40) NOT NULL,
  shipaddress    NVARCHAR(60) NOT NULL,
  shipcity       NVARCHAR(15) NOT NULL,
  shipregion     NVARCHAR(15) NULL,
  shippostalcode NVARCHAR(10) NULL,
  shipcountry    NVARCHAR(15) NOT NULL,
  CONSTRAINT PK_Orders PRIMARY KEY(orderid)
);
GO
 
INSERT INTO dbo.Orders SELECT * FROM Sales.Orders;
 
DELETE FROM dbo.Orders
  OUTPUT
    deleted.orderid,
    deleted.orderdate,
    deleted.empid,
    deleted.custid
WHERE orderdate < '20160101';
 
---------------------------------------------------------------------
-- UPDATE with OUTPUT
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.OrderDetails;
 
CREATE TABLE dbo.OrderDetails
(
  orderid   INT           NOT NULL,
  productid INT           NOT NULL,
  unitprice MONEY         NOT NULL
    CONSTRAINT DFT_OrderDetails_unitprice DEFAULT(0),
  qty       SMALLINT      NOT NULL
    CONSTRAINT DFT_OrderDetails_qty DEFAULT(1),
  discount  NUMERIC(4, 3) NOT NULL
    CONSTRAINT DFT_OrderDetails_discount DEFAULT(0),
  CONSTRAINT PK_OrderDetails PRIMARY KEY(orderid, productid),
  CONSTRAINT CHK_discount  CHECK (discount BETWEEN 0 AND 1),
  CONSTRAINT CHK_qty  CHECK (qty > 0),
  CONSTRAINT CHK_unitprice CHECK (unitprice >= 0)
);
GO
 
INSERT INTO dbo.OrderDetails SELECT * FROM Sales.OrderDetails;
 
UPDATE dbo.OrderDetails
  SET discount += 0.05
OUTPUT
  inserted.orderid,
  inserted.productid,
  deleted.discount AS olddiscount,
  inserted.discount AS newdiscount
WHERE productid = 51;
 
---------------------------------------------------------------------
-- MERGE with OUTPUT
---------------------------------------------------------------------
 
-- First, run Listing 8-2 to recreate Customers and CustomersStage
 
MERGE INTO dbo.Customers AS TGT
USING dbo.CustomersStage AS SRC
  ON TGT.custid = SRC.custid
WHEN MATCHED THEN
  UPDATE SET
    TGT.companyname = SRC.companyname,
    TGT.phone = SRC.phone,
    TGT.address = SRC.address
WHEN NOT MATCHED THEN
  INSERT (custid, companyname, phone, address)
  VALUES (SRC.custid, SRC.companyname, SRC.phone, SRC.address)
OUTPUT $action AS theaction, inserted.custid,
  deleted.companyname AS oldcompanyname,
  inserted.companyname AS newcompanyname,
  deleted.phone AS oldphone,
  inserted.phone AS newphone,
  deleted.address AS oldaddress,
  inserted.address AS newaddress;
 
---------------------------------------------------------------------
-- Nested DML
---------------------------------------------------------------------
 
DROP TABLE IF EXISTS dbo.ProductsAudit, dbo.Products;
 
CREATE TABLE dbo.Products
(
  productid    INT          NOT NULL,
  productname  NVARCHAR(40) NOT NULL,
  supplierid   INT          NOT NULL,
  categoryid   INT          NOT NULL,
  unitprice    MONEY        NOT NULL
    CONSTRAINT DFT_Products_unitprice DEFAULT(0),
  discontinued BIT          NOT NULL
    CONSTRAINT DFT_Products_discontinued DEFAULT(0),
  CONSTRAINT PK_Products PRIMARY KEY(productid),
  CONSTRAINT CHK_Products_unitprice CHECK(unitprice >= 0)
);
 
INSERT INTO dbo.Products SELECT * FROM Production.Products;
 
CREATE TABLE dbo.ProductsAudit
(
  LSN INT NOT NULL IDENTITY PRIMARY KEY,
  TS DATETIME2 NOT NULL DEFAULT(SYSDATETIME()),
  productid INT NOT NULL,
  colname SYSNAME NOT NULL,
  oldval SQL_VARIANT NOT NULL,
  newval SQL_VARIANT NOT NULL
);
 
INSERT INTO dbo.ProductsAudit(productid, colname, oldval, newval)
  SELECT productid, N'unitprice', oldval, newval
  FROM (UPDATE dbo.Products
          SET unitprice *= 1.15
        OUTPUT
          inserted.productid,
          deleted.unitprice AS oldval,
          inserted.unitprice AS newval
        WHERE supplierid = 1) AS D
  WHERE oldval < 20.0 AND newval >= 20.0;
 
SELECT * FROM dbo.ProductsAudit;
 
-- cleanup
DROP TABLE IF EXISTS dbo.OrderDetails, dbo.ProductsAudit, dbo.Products,
  dbo.Orders, dbo.Customers, dbo.T1, dbo.MySequences, dbo.CustomersStage;